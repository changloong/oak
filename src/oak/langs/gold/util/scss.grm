!*
-----------------------------------------------------------
Sassy Cascading Style Sheets
http://sass-lang.com/
-----------------------------------------------------------
*!

"Name"      = 'SCSS'
"Version"   = 'SCSS1 - 2011'
"Author"    = 'Chang Long'
"About"     = 'Sassy Cascading Style Sheets'
"Character Mapping" = 'Unicode'
"Case Sensitive"  = 'False'
"Auto Whitespace" = 'False'
"Start Symbol"    = <Nodes>

! --------------------------------------------------------------- Terminals

{ID Char}   = {AlphaNumeric} + [-]
{HexDigit}  = {Digit} + [abcdefABCDEF]
{String Char} = {All Valid} - ["\]
{AttrKey}    =  {Printable} - [=.#'''"'] - {Whitespace} - {#93}

String        = '' ({String Char} | '\'{All Valid})* '' | '"' ({String Char} | '\'{All Valid})* '"'

ID            = {Letter} ( {AlphaNumeric} | '-' {AlphaNumeric} )*

Number        = {Digit}+ ('.' {Digit}+)?
ColorRGB      = '#' {HexDigit}{HexDigit}{HexDigit} | '#' {HexDigit}{HexDigit}{HexDigit}{HexDigit}{HexDigit}{HexDigit}
Space         = {Whitespace}+

! -------------------------------------------------------------- Union

<TagName>       ::=  'html' | 'body' | 'div' | 'p' | 'pre' | 'center' 
                  | 'object' | 'frame' | 'iframe' | 'marquee'
                  | 'h1' | 'h2' | 'h3' | 'h4' | 'h5' | 'h6' | 'h7'
                  | 'hr'| 'br'
                  | 'table' | 'tbody' | 'thead' | 'tfoot' | 'tr' | 'th' | 'td' 
                  | 'img' | 'area'
                  | 'ul' | 'ol' | 'li'   | 'dd' | 'dt' | 'dl'
                  | 'a' | 'b' | 'i' | 'u' | 's' | 'em' | 'span' | 'font' | 'small' | 'big' | 'strike' | 'strong'
                  | 'form' | 'input' | 'select' | 'option' | 'optgroup' | 'textarea'
                  | 'fieldset' | 'legend' | 'label'
                  | '&' | '*'

<Pseudo>      ::= ':active' | ':visited'  | ':hover'  | ':link'
                  | ':first-letter'  | ':first-line'
                  | ':first-child' | ':left' | ':right'
                  | ':focus'

<Unit>       ::= 'em'    ! The height of the element's font.
            | 'ex'    ! The height of the letter "x"
            | 'px'    ! Pixels
            | '%'   ! Percentage
            | 'in'    ! Inches
            | 'cm'    ! Centimeters
            | 'mm'    ! Millimeters. 
            | 'pt'    ! Points (72 points = 6 picas = 1 inch) 
            | 'pc'    ! Picas (1 pica = 12 points)

! --------------------------------------------------------------- Scss Script

<iSpace>     ::= | Space

<iExp>       ::= '#{$' ID '}'
                 | '#{' <Add Exp> '}'

<IDString>   ::= ID  | <iExp> |  ID  

<AttrKey> ::= <IDString> | String

<String>  ::= <IDString> | String

! --------------------------------------------------------------- Tag

<TagID>       ::=  '#' <IDString>

<ClassID>     ::=  '.' <IDString> 
                  | '.' <IDString> <ClassID> 

<TagAttr>       ::= '[' <iSpace> <AttrKey> <iSpace> ']'
                  | '[' <iSpace> <AttrKey> <iSpace> '=' <iSpace>  <String> <iSpace> ']'
                  | '[' <iSpace> <AttrKey> <iSpace> '~=' <iSpace> <String> <iSpace> ']'
                  | '[' <iSpace> <AttrKey> <iSpace> '|=' <iSpace> <String> <iSpace>  ']'

<TagAttrs>      ::= <TagAttr> 
                 | <TagAttr> <TagAttrs> 

<TagNode>   ::=   <TagName> 
                  | <TagID> 
                  | <ClassID>
                  | <TagName> <TagID>     
                  | <TagName> <ClassID> 
                  | <TagName> <TagID> <ClassID>  
                  | <TagID> <ClassID> 
                  | <TagNode> <Pseudo> 
                  | <TagNode> <TagAttrs> 
                  | <TagNode> <TagAttrs> <Pseudo> 

<TagPath>   ::= <TagNode>
                 | <TagNode> '>' <TagNode>
                 | <TagNode> Space <TagNode>

<TagPaths>   ::=  <TagPath>
                 | <TagPath> ',' <TagPaths>

! --------------------------------------------------------------- Css Rule
<size-value>     ::= 'normal' | 'medium' | <Number>
<color>          ::= 'red' | 'blue' | 'green' | 'yellow' | 'white'
                    | ColorRGB 
                    | 'rgb(' <Number> <iSpace> ',' <Number> <iSpace> ','  <Number> <iSpace> ')'

<font-style-value> ::= 'normal' | 'italic' | 'oblique'
<font-variant-value> ::= 'normal' | 'small-caps'
<font-weight-value>  ::= 'normal' | 'bold' | 'bolder' | 'lighter' | '100' | '200' | '300' | '400' | '500' | '600' | '700' | Var
<font-family-value-item> ::= ID | String 
<font-family-value> ::= <font-family-value-item> | <font-family-value-item> <iSpace> ',' <iSpace> <font-family-value>
<font-size-adjust-value> ::= 'none' | <Number>
<font-stretch-value> ::= 'normal' | 'narrower' | 'wider' | 'ultra-condensed' | 'extra-condensed' | 'condensed' | 'semi-condensed' | 'semi-expanded' | 'expanded' | 'extra-expanded' | 'ultra-expanded'
<text-decoration-value-item> ::= 'none' | 'underline' | 'blink' | 'overline' | 'line-through'  
<text-decoration-value> ::= <text-decoration-value-item> | <text-decoration-value-item> Space <text-decoration-value>
<text-underline-position-value> ::= 'below' | 'above' 
<text-transform-value> ::= 'none' | 'capitalize' | 'uppercase' | 'lowercase' 

<font-item> ::=  <iSpace> 'size' <iSpace> ':' <iSpace>  <size-value> <iSpace> ';' 
                 |  <iSpace> 'style'  <iSpace> ':' <iSpace> <font-style-value> <iSpace> ';' 
                 |  <iSpace> 'weight' <iSpace> ':' <iSpace> <font-weight-value> <iSpace> ';' 
                 |  <iSpace> 'family' <iSpace> ':' <iSpace> <font-family-value> <iSpace> ';' 
                 |  <iSpace> 'variant' <iSpace> ':' <iSpace> <font-variant-value> <iSpace> ';' 
                 |  <iSpace> 'stretch' <iSpace> ':' <iSpace> <font-stretch-value> <iSpace> ';' 
                 |  <iSpace> 'size-adjust' <iSpace> ':' <iSpace> <font-size-adjust-value> <iSpace> ';' 

<font-item-value> ::= <font-item>  <iSpace> | <font-item> <font-item-value>

<font-item-size-value> ::=  <iNumber> 
                  |  <iNumber>  <iSpace> '/' <iSpace> <iNumber> 

<font-body>  ::=  '{' <font-item-value> '}'
                 | <font-item-size-value> <iSpace>  '{' <font-item-value> '}'
                 | <font-item-size-value> 
                 | <font-style-value> Space <font-item-size-value> 
                 | <font-style-value> Space <font-variant-value> Space <font-weight-value> Space <font-item-size-value> 
                 | <font-style-value> Space <font-variant-value> Space <font-weight-value> Space <font-item-size-value> <font-family-value>

<Rule>  ::=       <iSpace> 'font-style' <iSpace> ':' <iSpace> <font-style-value>   <iSpace> ';' 
                 | <iSpace> 'font-variant' <iSpace> ':' <iSpace> <font-variant-value>  <iSpace> ';' 
                 | <iSpace> 'font-weight' <iSpace> ':' <iSpace> <font-weight-value>  <iSpace> ';' 
                 | <iSpace> 'font-size' <iSpace> ':' <iSpace> <size-value>  <iSpace> ';' 
                 | <iSpace> 'font-family' <iSpace> ':' <iSpace> <font-family-value>  <iSpace> ';' 
                 | <iSpace> 'font-size-adjust' <iSpace> ':' <iSpace> <font-size-adjust-value>  <iSpace> ';' 
                 | <iSpace> 'font-stretch' <iSpace> ':' <iSpace> <font-stretch-value>  <iSpace> ';' 
                 | <iSpace> 'font' <iSpace> ':' <iSpace> <font-body> 

                 | <iSpace> 'line-height' <iSpace> ':' <iSpace> <size-value>  <iSpace> ';' 
                 | <iSpace> 'letter-spacing'  <iSpace> ':' <iSpace> <size-value>  <iSpace> ';' 
                 | <iSpace> 'word-spacing'  <iSpace> ':' <iSpace> <size-value>  <iSpace> ';' 
                 | <iSpace> 'color'  <iSpace> ':' <iSpace> <color>  <iSpace> ';' 

                 | <iSpace> 'text-decoration' <iSpace> ':' <iSpace> <text-decoration-value>  <iSpace> ';' 
                 | <iSpace> 'text-underline-position' <iSpace> ':' <iSpace> <text-underline-position-value>  <iSpace> ';' 
                 | <iSpace> 'text-transform' <iSpace> ':' <iSpace> <text-transform-value>  <iSpace> ';' 


<Rules> ::=   <Rule> <iSpace>
             | <Rule> <Rules> 

<Node> ::=  <iSpace> <TagPaths> <iSpace>  '{' <Rules> '}' 

<Nodes> ::= <Node> <iSpace> 
           | <Node> <Nodes>

! --------------------------------------------------------------- Expression

<Add Exp>     ::= <Add Exp> <iSpace> '+' <Mult Exp>
               |  <Add Exp> <iSpace> '-'  <Mult Exp>
               |  <Mult Exp>

<Mult Exp>    ::=  <Mult Exp> <iSpace> '*' <Negate Exp>
               |  <Mult Exp> <iSpace> '/'  <Negate Exp>
               |  <Mult Exp>  <iSpace> '%'  <Negate Exp>
               |  <Negate Exp>

<Negate Exp>  ::=  <iSpace>  '-' <Number>
               |   <iSpace> <Number> 

<iNumber>     ::= Number
               | Number <Unit>
               | Number Space <Unit>
               | Var
               | Var Space <Unit>
               | <iExp>
               | <iExp> Space <Unit>

<Number>       ::= <iNumber>
               |   <iSpace> '(' <Add Exp> ')'

